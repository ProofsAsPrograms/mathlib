/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import analysis.normed_space.basic
import analysis.normed_space.linear_isometry

/-! # Constructions of continuous linear maps between (semi-)normed spaces

A fundamental fact about (semi-)linear maps between normed spaces over sensible fields is that
continuity and boundedness are equivalent conditions.  That is, for normed spaces `E`, `F`, a
`linear_map` `f : E â†’â‚›â‚—[Ïƒ] F` is the coercion of some `continuous_linear_map` `f' : E â†’SL[Ïƒ] F`, if
and only if there exists a bound `C` such that for all `x`, `â€–f xâ€– â‰¤ C * â€–xâ€–`.

We prove one direction in this file: `linear_map.mk_continuous`, boundedness implies continuity. The
other direction, `continuous_linear_map.bound`, is deferred to a later file, where the
strong operator topology on `E â†’SL[Ïƒ] F` is available, because it is natural to use
`continuous_linear_map.bound` to define a norm `â¨† x, â€–f xâ€– / â€–xâ€–` on `E â†’SL[Ïƒ] F` and to show that
this is compatible with the strong operator topology.

This file also contains several corollaries of `linear_map.mk_continuous`: other "easy"
constructions of continuous linear maps between normed spaces.

This file is meant to be lightweight (it is imported by much of the analysis library); think twice
before adding imports!
-/

open metric continuous_linear_map
open set real

open_locale nnreal

variables {ğ•œ ğ•œâ‚‚ E F G ğ“• : Type*}

section
variables [normed_field ğ•œ] [normed_field ğ•œâ‚‚]

/-! ## For a general normed field

### General constructions -/

section seminormed

variables [seminormed_add_comm_group E] [seminormed_add_comm_group F] [seminormed_add_comm_group G]
variables [normed_space ğ•œ E] [normed_space ğ•œâ‚‚ F] [normed_space ğ•œ G]
variables {Ïƒ : ğ•œ â†’+* ğ•œâ‚‚} (f : E â†’â‚›â‚—[Ïƒ] F)

/-- Construct a continuous linear map from a linear map and a bound on this linear map.
The fact that the norm of the continuous linear map is then controlled is given in
`linear_map.mk_continuous_norm_le`. -/
def linear_map.mk_continuous (C : â„) (h : âˆ€x, â€–f xâ€– â‰¤ C * â€–xâ€–) : E â†’SL[Ïƒ] F :=
âŸ¨f, add_monoid_hom_class.continuous_of_bound f C hâŸ©

/-- Reinterpret a linear map `ğ•œ â†’â‚—[ğ•œ] E` as a continuous linear map. This construction
is generalized to the case of any finite dimensional domain
in `linear_map.to_continuous_linear_map`. -/
def linear_map.to_continuous_linear_mapâ‚ (f : ğ•œ â†’â‚—[ğ•œ] E) : ğ•œ â†’L[ğ•œ] E :=
f.mk_continuous (â€–f 1â€–) $ Î» x, le_of_eq $
by { conv_lhs { rw â† mul_one x }, rw [â† smul_eq_mul, f.map_smul, norm_smul, mul_comm] }

/-- Construct a continuous linear map from a linear map and the existence of a bound on this linear
map. If you have an explicit bound, use `linear_map.mk_continuous` instead, as a norm estimate will
follow automatically in `linear_map.mk_continuous_norm_le`. -/
def linear_map.mk_continuous_of_exists_bound (h : âˆƒC, âˆ€x, â€–f xâ€– â‰¤ C * â€–xâ€–) : E â†’SL[Ïƒ] F :=
âŸ¨f, let âŸ¨C, hCâŸ© := h in add_monoid_hom_class.continuous_of_bound f C hCâŸ©

lemma continuous_of_linear_of_boundâ‚›â‚— {f : E â†’ F} (h_add : âˆ€ x y, f (x + y) = f x + f y)
  (h_smul : âˆ€ (c : ğ•œ) x, f (c â€¢ x) = (Ïƒ c) â€¢ f x) {C : â„} (h_bound : âˆ€ x, â€–f xâ€– â‰¤ C*â€–xâ€–) :
  continuous f :=
let Ï† : E â†’â‚›â‚—[Ïƒ] F := { to_fun := f, map_add' := h_add, map_smul' := h_smul } in
add_monoid_hom_class.continuous_of_bound Ï† C h_bound

lemma continuous_of_linear_of_bound {f : E â†’ G} (h_add : âˆ€ x y, f (x + y) = f x + f y)
  (h_smul : âˆ€ (c : ğ•œ) x, f (c â€¢ x) = c â€¢ f x) {C : â„} (h_bound : âˆ€ x, â€–f xâ€– â‰¤ C*â€–xâ€–) :
  continuous f :=
let Ï† : E â†’â‚—[ğ•œ] G := { to_fun := f, map_add' := h_add, map_smul' := h_smul } in
add_monoid_hom_class.continuous_of_bound Ï† C h_bound

@[simp, norm_cast] lemma linear_map.mk_continuous_coe (C : â„) (h : âˆ€x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
  ((f.mk_continuous C h) : E â†’â‚›â‚—[Ïƒ] F) = f := rfl

@[simp] lemma linear_map.mk_continuous_apply (C : â„) (h : âˆ€x, â€–f xâ€– â‰¤ C * â€–xâ€–) (x : E) :
  f.mk_continuous C h x = f x := rfl

@[simp, norm_cast] lemma linear_map.mk_continuous_of_exists_bound_coe
  (h : âˆƒC, âˆ€x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
  ((f.mk_continuous_of_exists_bound h) : E â†’â‚›â‚—[Ïƒ] F) = f := rfl

@[simp] lemma linear_map.mk_continuous_of_exists_bound_apply (h : âˆƒC, âˆ€x, â€–f xâ€– â‰¤ C * â€–xâ€–) (x : E) :
  f.mk_continuous_of_exists_bound h x = f x := rfl

@[simp] lemma linear_map.to_continuous_linear_mapâ‚_coe (f : ğ•œ â†’â‚—[ğ•œ] E) :
  (f.to_continuous_linear_mapâ‚ : ğ•œ â†’â‚—[ğ•œ] E) = f :=
rfl

@[simp] lemma linear_map.to_continuous_linear_mapâ‚_apply (f : ğ•œ â†’â‚—[ğ•œ] E) (x) :
  f.to_continuous_linear_mapâ‚ x = f x :=
rfl

namespace continuous_linear_map

theorem antilipschitz_of_bound (f : E â†’SL[Ïƒ] F) {K : â„â‰¥0} (h : âˆ€ x, â€–xâ€– â‰¤ K * â€–f xâ€–) :
  antilipschitz_with K f :=
add_monoid_hom_class.antilipschitz_of_bound _ h

lemma bound_of_antilipschitz (f : E â†’SL[Ïƒ] F) {K : â„â‰¥0} (h : antilipschitz_with K f) (x) :
  â€–xâ€– â‰¤ K * â€–f xâ€– :=
add_monoid_hom_class.bound_of_antilipschitz _ h x

end continuous_linear_map

section

variables {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [ring_hom_inv_pair Ïƒ Ïƒâ‚‚â‚] [ring_hom_inv_pair Ïƒâ‚‚â‚ Ïƒ]

include Ïƒâ‚‚â‚

/-- Construct a continuous linear equivalence from a linear equivalence together with
bounds in both directions. -/
def linear_equiv.to_continuous_linear_equiv_of_bounds (e : E â‰ƒâ‚›â‚—[Ïƒ] F) (C_to C_inv : â„)
  (h_to : âˆ€ x, â€–e xâ€– â‰¤ C_to * â€–xâ€–) (h_inv : âˆ€ x : F, â€–e.symm xâ€– â‰¤ C_inv * â€–xâ€–) : E â‰ƒSL[Ïƒ] F :=
{ to_linear_equiv := e,
  continuous_to_fun := add_monoid_hom_class.continuous_of_bound e C_to h_to,
  continuous_inv_fun := add_monoid_hom_class.continuous_of_bound e.symm C_inv h_inv }

end

end seminormed

section normed

variables [normed_add_comm_group E] [normed_add_comm_group F] [normed_space ğ•œ E] [normed_space ğ•œâ‚‚ F]
variables {Ïƒ : ğ•œ â†’+* ğ•œâ‚‚} (f g : E â†’SL[Ïƒ] F) (x y z : E)

theorem continuous_linear_map.uniform_embedding_of_bound {K : â„â‰¥0} (hf : âˆ€ x, â€–xâ€– â‰¤ K * â€–f xâ€–) :
  uniform_embedding f :=
(add_monoid_hom_class.antilipschitz_of_bound f hf).uniform_embedding f.uniform_continuous

end normed

/-! # Homotheties -/

section seminormed

variables [seminormed_add_comm_group E] [seminormed_add_comm_group F]
variables [normed_space ğ•œ E] [normed_space ğ•œâ‚‚ F]
variables {Ïƒ : ğ•œ â†’+* ğ•œâ‚‚} (f : E â†’â‚›â‚—[Ïƒ] F)

/-- A (semi-)linear map which is a homothety is a continuous linear map.
    Since the field `ğ•œ` need not have `â„` as a subfield, this theorem is not directly deducible from
    the corresponding theorem about isometries plus a theorem about scalar multiplication.  Likewise
    for the other theorems about homotheties in this file.
 -/
def continuous_linear_map.of_homothety (f : E â†’â‚›â‚—[Ïƒ] F) (a : â„) (hf : âˆ€x, â€–f xâ€– = a * â€–xâ€–) :
  E â†’SL[Ïƒ] F :=
f.mk_continuous a (Î» x, le_of_eq (hf x))

variables {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [ring_hom_inv_pair Ïƒ Ïƒâ‚‚â‚] [ring_hom_inv_pair Ïƒâ‚‚â‚ Ïƒ]

include Ïƒâ‚‚â‚

lemma continuous_linear_equiv.homothety_inverse (a : â„) (ha : 0 < a) (f : E â‰ƒâ‚›â‚—[Ïƒ] F) :
  (âˆ€ (x : E), â€–f xâ€– = a * â€–xâ€–) â†’ (âˆ€ (y : F), â€–f.symm yâ€– = aâ»Â¹ * â€–yâ€–) :=
begin
  intros hf y,
  calc â€–(f.symm) yâ€– = aâ»Â¹ * (a * â€– (f.symm) yâ€–) : _
  ... =  aâ»Â¹ * â€–f ((f.symm) y)â€– : by rw hf
  ... = aâ»Â¹ * â€–yâ€– : by simp,
  rw [â† mul_assoc, inv_mul_cancel (ne_of_lt ha).symm, one_mul],
end

/-- A linear equivalence which is a homothety is a continuous linear equivalence. -/
noncomputable def continuous_linear_equiv.of_homothety (f : E â‰ƒâ‚›â‚—[Ïƒ] F) (a : â„) (ha : 0 < a)
  (hf : âˆ€x, â€–f xâ€– = a * â€–xâ€–) :
  E â‰ƒSL[Ïƒ] F :=
linear_equiv.to_continuous_linear_equiv_of_bounds f a aâ»Â¹
  (Î» x, (hf x).le) (Î» x, (continuous_linear_equiv.homothety_inverse a ha f hf x).le)

end seminormed

/-! # The span of a single vector -/

section seminormed

variables [seminormed_add_comm_group E] [normed_space ğ•œ E]

namespace linear_isometry
variables (ğ•œ E)

/-- Given a unit-length element `x` of a normed space `E` over a field `ğ•œ`, the natural linear
    isometry map from `ğ•œ` to `E` by taking multiples of `x`.-/
def to_span_singleton {v : E} (hv : â€–vâ€– = 1) : ğ•œ â†’â‚—áµ¢[ğ•œ] E :=
{ norm_map' := Î» x, by simp [norm_smul, hv],
  .. linear_map.to_span_singleton ğ•œ E v }
variables {ğ•œ E}

@[simp] lemma to_span_singleton_apply {v : E} (hv : â€–vâ€– = 1) (a : ğ•œ) :
  to_span_singleton ğ•œ E hv a = a â€¢ v :=
rfl

@[simp] lemma coe_to_span_singleton {v : E} (hv : â€–vâ€– = 1) :
  (to_span_singleton ğ•œ E hv).to_linear_map = linear_map.to_span_singleton ğ•œ E v :=
rfl

end linear_isometry

namespace continuous_linear_map

variable (ğ•œ)

lemma to_span_singleton_homothety (x : E) (c : ğ•œ) :
  â€–linear_map.to_span_singleton ğ•œ E x câ€– = â€–xâ€– * â€–câ€– :=
by {rw mul_comm, exact norm_smul _ _}

/-- Given an element `x` of a normed space `E` over a field `ğ•œ`, the natural continuous
    linear map from `ğ•œ` to `E` by taking multiples of `x`.-/
def to_span_singleton (x : E) : ğ•œ â†’L[ğ•œ] E :=
of_homothety (linear_map.to_span_singleton ğ•œ E x) â€–xâ€– (to_span_singleton_homothety ğ•œ x)

lemma to_span_singleton_apply (x : E) (r : ğ•œ) : to_span_singleton ğ•œ x r = r â€¢ x :=
by simp [to_span_singleton, of_homothety, linear_map.to_span_singleton]

lemma to_span_singleton_add (x y : E) :
  to_span_singleton ğ•œ (x + y) = to_span_singleton ğ•œ x + to_span_singleton ğ•œ y :=
by { ext1, simp [to_span_singleton_apply], }

lemma to_span_singleton_smul' (ğ•œ') [normed_field ğ•œ'] [normed_space ğ•œ' E]
  [smul_comm_class ğ•œ ğ•œ' E] (c : ğ•œ') (x : E) :
  to_span_singleton ğ•œ (c â€¢ x) = c â€¢ to_span_singleton ğ•œ x :=
by { ext1, rw [to_span_singleton_apply, smul_apply, to_span_singleton_apply, smul_comm], }

lemma to_span_singleton_smul (c : ğ•œ) (x : E) :
  to_span_singleton ğ•œ (c â€¢ x) = c â€¢ to_span_singleton ğ•œ x :=
to_span_singleton_smul' ğ•œ ğ•œ c x

end continuous_linear_map

section

namespace continuous_linear_equiv

variable (ğ•œ)

lemma to_span_nonzero_singleton_homothety (x : E) (h : x â‰  0) (c : ğ•œ) :
  â€–linear_equiv.to_span_nonzero_singleton ğ•œ E x h câ€– = â€–xâ€– * â€–câ€– :=
continuous_linear_map.to_span_singleton_homothety _ _ _

end continuous_linear_equiv

end

end seminormed

section normed

variables [normed_add_comm_group E] [normed_space ğ•œ E]

namespace continuous_linear_equiv
variable (ğ•œ)

/-- Given a nonzero element `x` of a normed space `Eâ‚` over a field `ğ•œ`, the natural
    continuous linear equivalence from `Eâ‚` to the span of `x`.-/
noncomputable def to_span_nonzero_singleton (x : E) (h : x â‰  0) : ğ•œ â‰ƒL[ğ•œ] (ğ•œ âˆ™ x) :=
of_homothety
  (linear_equiv.to_span_nonzero_singleton ğ•œ E x h)
  â€–xâ€–
  (norm_pos_iff.mpr h)
  (to_span_nonzero_singleton_homothety ğ•œ x h)

/-- Given a nonzero element `x` of a normed space `Eâ‚` over a field `ğ•œ`, the natural continuous
    linear map from the span of `x` to `ğ•œ`.-/
noncomputable def coord (x : E) (h : x â‰  0) : (ğ•œ âˆ™ x) â†’L[ğ•œ] ğ•œ :=
  (to_span_nonzero_singleton ğ•œ x h).symm

@[simp] lemma coe_to_span_nonzero_singleton_symm {x : E} (h : x â‰  0) :
  â‡‘(to_span_nonzero_singleton ğ•œ x h).symm = coord ğ•œ x h := rfl

@[simp] lemma coord_to_span_nonzero_singleton {x : E} (h : x â‰  0) (c : ğ•œ) :
  coord ğ•œ x h (to_span_nonzero_singleton ğ•œ x h c) = c :=
(to_span_nonzero_singleton ğ•œ x h).symm_apply_apply c

@[simp] lemma to_span_nonzero_singleton_coord {x : E} (h : x â‰  0) (y : ğ•œ âˆ™ x) :
  to_span_nonzero_singleton ğ•œ x h (coord ğ•œ x h y) = y :=
(to_span_nonzero_singleton ğ•œ x h).apply_symm_apply y

@[simp] lemma coord_self (x : E) (h : x â‰  0) :
  (coord ğ•œ x h) (âŸ¨x, submodule.mem_span_singleton_self xâŸ© : ğ•œ âˆ™ x) = 1 :=
linear_equiv.coord_self ğ•œ E x h

end continuous_linear_equiv

end normed

end

/-! ## For a nontrivially normed field -/

section
variables [nontrivially_normed_field ğ•œ] [nontrivially_normed_field ğ•œâ‚‚]

section semi_normed

variables [seminormed_add_comm_group E] [seminormed_add_comm_group F]
  [normed_space ğ•œ E] [normed_space ğ•œâ‚‚ F] {Ïƒ : ğ•œ â†’+* ğ•œâ‚‚}

/-- If `â€–xâ€– = 0` and `f` is continuous then `â€–f xâ€– = 0`. -/
lemma norm_image_of_norm_zero [semilinear_map_class ğ“• Ïƒ E F] (f : ğ“•)
  (hf : continuous f) {x : E} (hx : â€–xâ€– = 0) : â€–f xâ€– = 0 :=
begin
  refine le_antisymm (le_of_forall_pos_le_add (Î» Îµ hÎµ, _)) (norm_nonneg (f x)),
  rcases normed_add_comm_group.tendsto_nhds_nhds.1 (hf.tendsto 0) Îµ hÎµ with âŸ¨Î´, Î´_pos, hÎ´âŸ©,
  replace hÎ´ := hÎ´ x,
  rw [sub_zero, hx] at hÎ´,
  replace hÎ´ := le_of_lt (hÎ´ Î´_pos),
  rw [map_zero, sub_zero] at hÎ´,
  rwa [zero_add]
end

variables [ring_hom_isometric Ïƒ]

lemma semilinear_map_class.bound_of_shell_semi_normed [semilinear_map_class ğ“• Ïƒ E F]
  (f : ğ“•) {Îµ C : â„} (Îµ_pos : 0 < Îµ) {c : ğ•œ} (hc : 1 < â€–câ€–)
  (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) {x : E} (hx : â€–xâ€– â‰  0) :
  â€–f xâ€– â‰¤ C * â€–xâ€– :=
begin
  rcases rescale_to_shell_semi_normed hc Îµ_pos hx with âŸ¨Î´, hÎ´, Î´xle, leÎ´x, Î´invâŸ©,
  have := hf (Î´ â€¢ x) leÎ´x Î´xle,
  simpa only [map_smulâ‚›â‚—, norm_smul, mul_left_comm C, mul_le_mul_left (norm_pos_iff.2 hÎ´),
              ring_hom_isometric.is_iso] using hf (Î´ â€¢ x) leÎ´x Î´xle
end

/-- A continuous linear map between seminormed spaces is bounded when the field is nontrivially
normed. The continuity ensures boundedness on a ball of some radius `Îµ`. The nontriviality of the
norm is then used to rescale any element into an element of norm in `[Îµ/C, Îµ]`, whose image has a
controlled norm. The norm control for the original element follows by rescaling. -/
lemma semilinear_map_class.bound_of_continuous [semilinear_map_class ğ“• Ïƒ E F] (f : ğ“•)
  (hf : continuous f) : âˆƒ C, 0 < C âˆ§ (âˆ€ x : E, â€–f xâ€– â‰¤ C * â€–xâ€–) :=
begin
  rcases normed_add_comm_group.tendsto_nhds_nhds.1 (hf.tendsto 0) 1 zero_lt_one with âŸ¨Îµ, Îµ_pos, hÎµâŸ©,
  simp only [sub_zero, map_zero] at hÎµ,
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  have : 0 < â€–câ€– / Îµ, from div_pos (zero_lt_one.trans hc) Îµ_pos,
  refine âŸ¨â€–câ€– / Îµ, this, Î» x, _âŸ©,
  by_cases hx : â€–xâ€– = 0,
  { rw [hx, mul_zero],
    exact le_of_eq (norm_image_of_norm_zero f hf hx) },
  refine semilinear_map_class.bound_of_shell_semi_normed f Îµ_pos hc (Î» x hle hlt, _) hx,
  refine (hÎµ _ hlt).le.trans _,
  rwa [â† div_le_iff' this, one_div_div]
end

theorem continuous_linear_map.bound (f : E â†’SL[Ïƒ] F) :
  âˆƒ C, 0 < C âˆ§ (âˆ€ x : E, â€–f xâ€– â‰¤ C * â€–xâ€–) :=
semilinear_map_class.bound_of_continuous f f.2

end semi_normed

section normed
variables [normed_add_comm_group E] [normed_add_comm_group F] [normed_add_comm_group G]

variables
  [normed_space ğ•œ E] [normed_space ğ•œâ‚‚ F] [normed_space ğ•œ G]
  {Ïƒ : ğ•œ â†’+* ğ•œâ‚‚}
  (f g : E â†’SL[Ïƒ] F) (x y z : E)

lemma linear_map.bound_of_shell [ring_hom_isometric Ïƒ] (f : E â†’â‚›â‚—[Ïƒ] F) {Îµ C : â„}
  (Îµ_pos : 0 < Îµ) {c : ğ•œ} (hc : 1 < â€–câ€–)
  (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) (x : E) :
  â€–f xâ€– â‰¤ C * â€–xâ€– :=
begin
  by_cases hx : x = 0, { simp [hx] },
  exact semilinear_map_class.bound_of_shell_semi_normed f Îµ_pos hc hf
    (ne_of_lt (norm_pos_iff.2 hx)).symm
end

/--
`linear_map.bound_of_ball_bound'` is a version of this lemma over a field satisfying `is_R_or_C`
that produces a concrete bound.
-/
lemma linear_map.bound_of_ball_bound {r : â„} (r_pos : 0 < r) (c : â„) (f : E â†’â‚—[ğ•œ] G)
  (h : âˆ€ z âˆˆ metric.ball (0 : E) r, â€–f zâ€– â‰¤ c) :
  âˆƒ C, âˆ€ (z : E), â€–f zâ€– â‰¤ C * â€–zâ€– :=
begin
  cases @nontrivially_normed_field.non_trivial ğ•œ _ with k hk,
  use c * (â€–kâ€– / r),
  intro z,
  refine linear_map.bound_of_shell _ r_pos hk (Î» x hko hxo, _) _,
  calc â€–f xâ€– â‰¤ c : h _ (mem_ball_zero_iff.mpr hxo)
         ... â‰¤ c * ((â€–xâ€– * â€–kâ€–) / r) : le_mul_of_one_le_right _ _
         ... = _ : by ring,
  { exact le_trans (norm_nonneg _) (h 0 (by simp [r_pos])) },
  { rw [div_le_iff (zero_lt_one.trans hk)] at hko,
    exact (one_le_div r_pos).mpr hko }
end

namespace continuous_linear_map

/-- If a continuous linear map is a uniform embedding, then it is expands the distances
by a positive factor.-/
theorem antilipschitz_of_uniform_embedding (f : E â†’L[ğ•œ] G) (hf : uniform_embedding f) :
  âˆƒ K, antilipschitz_with K f :=
begin
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ (Îµ : â„) (H : Îµ > 0), âˆ€ {x y : E}, dist (f x) (f y) < Îµ â†’ dist x y < 1,
    from (uniform_embedding_iff.1 hf).2.2 1 zero_lt_one,
  let Î´ := Îµ/2,
  have Î´_pos : Î´ > 0 := half_pos Îµpos,
  have H : âˆ€{x}, â€–f xâ€– â‰¤ Î´ â†’ â€–xâ€– â‰¤ 1,
  { assume x hx,
    have : dist x 0 â‰¤ 1,
    { refine (hÎµ _).le,
      rw [f.map_zero, dist_zero_right],
      exact hx.trans_lt (half_lt_self Îµpos) },
    simpa using this },
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  refine âŸ¨âŸ¨Î´â»Â¹, _âŸ© * â€–câ€–â‚Š, add_monoid_hom_class.antilipschitz_of_bound f $ Î»x, _âŸ©,
  exact inv_nonneg.2 (le_of_lt Î´_pos),
  by_cases hx : f x = 0,
  { have : f x = f 0, by { simp [hx] },
    have : x = 0 := (uniform_embedding_iff.1 hf).1 this,
    simp [this] },
  { rcases rescale_to_shell hc Î´_pos hx with âŸ¨d, hd, dxlt, ledx, dinvâŸ©,
    rw [â† f.map_smul d] at dxlt,
    have : â€–d â€¢ xâ€– â‰¤ 1 := H dxlt.le,
    calc â€–xâ€– = â€–dâ€–â»Â¹ * â€–d â€¢ xâ€– :
      by rwa [â† norm_inv, â† norm_smul, â† mul_smul, inv_mul_cancel, one_smul]
    ... â‰¤ â€–dâ€–â»Â¹ * 1 :
      mul_le_mul_of_nonneg_left this (inv_nonneg.2 (norm_nonneg _))
    ... â‰¤ Î´â»Â¹ * â€–câ€– * â€–f xâ€– :
      by rwa [mul_one] }
end

end continuous_linear_map

end normed

end
