/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import measure_theory.function.lp_space.basic
import topology.continuous_function.compact

/-! # Continuous functions in `Lp` -/

noncomputable theory
open topological_space measure_theory filter
open_locale nnreal ennreal big_operators topological_space measure_theory

variables {Î± E F G : Type*} {m m0 : measurable_space Î±} {p : â„â‰¥0âˆ} {q : â„} {Î¼ Î½ : measure Î±}
  [normed_add_comm_group E] [normed_add_comm_group F] [normed_add_comm_group G]

open_locale bounded_continuous_function
open bounded_continuous_function

section

variables [topological_space Î±] [borel_space Î±] [second_countable_topology_either Î± E]
variables (E p Î¼)

/-- An additive subgroup of `Lp E p Î¼`, consisting of the equivalence classes which contain a
bounded continuous representative. -/
def measure_theory.Lp.bounded_continuous_function : add_subgroup (Lp E p Î¼) :=
add_subgroup.add_subgroup_of
  ((continuous_map.to_ae_eq_fun_add_hom Î¼).comp (to_continuous_map_add_hom Î± E)).range
  (Lp E p Î¼)

variables {E p Î¼}

/-- By definition, the elements of `Lp.bounded_continuous_function E p Î¼` are the elements of
`Lp E p Î¼` which contain a bounded continuous representative. -/
lemma measure_theory.Lp.mem_bounded_continuous_function_iff {f : (Lp E p Î¼)} :
  f âˆˆ measure_theory.Lp.bounded_continuous_function E p Î¼
    â†” âˆƒ fâ‚€ : (Î± â†’áµ‡ E), fâ‚€.to_continuous_map.to_ae_eq_fun Î¼ = (f : Î± â†’â‚˜[Î¼] E) :=
add_subgroup.mem_add_subgroup_of

namespace bounded_continuous_function

variables [is_finite_measure Î¼]

/-- A bounded continuous function on a finite-measure space is in `Lp`. -/
lemma mem_Lp (f : Î± â†’áµ‡ E) :
  f.to_continuous_map.to_ae_eq_fun Î¼ âˆˆ Lp E p Î¼ :=
begin
  refine Lp.mem_Lp_of_ae_bound (â€–fâ€–) _,
  filter_upwards [f.to_continuous_map.coe_fn_to_ae_eq_fun Î¼] with x _,
  convert f.norm_coe_le_norm x
end

/-- The `Lp`-norm of a bounded continuous function is at most a constant (depending on the measure
of the whole space) times its sup-norm. -/
lemma Lp_norm_le (f : Î± â†’áµ‡ E) :
  â€–(âŸ¨f.to_continuous_map.to_ae_eq_fun Î¼, mem_Lp fâŸ© : Lp E p Î¼)â€–
  â‰¤ (measure_univ_nnreal Î¼) ^ (p.to_real)â»Â¹ * â€–fâ€– :=
begin
  apply Lp.norm_le_of_ae_bound (norm_nonneg f),
  { refine (f.to_continuous_map.coe_fn_to_ae_eq_fun Î¼).mono _,
    intros x hx,
    convert f.norm_coe_le_norm x },
  { apply_instance }
end

variables (p Î¼)

/-- The normed group homomorphism of considering a bounded continuous function on a finite-measure
space as an element of `Lp`. -/
def to_Lp_hom [fact (1 â‰¤ p)] : normed_add_group_hom (Î± â†’áµ‡ E) (Lp E p Î¼) :=
{ bound' := âŸ¨_, Lp_norm_leâŸ©,
  .. add_monoid_hom.cod_restrict
      ((continuous_map.to_ae_eq_fun_add_hom Î¼).comp (to_continuous_map_add_hom Î± E))
      (Lp E p Î¼)
      mem_Lp }

lemma range_to_Lp_hom [fact (1 â‰¤ p)] :
  ((to_Lp_hom p Î¼).range : add_subgroup (Lp E p Î¼))
    = measure_theory.Lp.bounded_continuous_function E p Î¼ :=
begin
  symmetry,
  convert add_monoid_hom.add_subgroup_of_range_eq_of_le
    ((continuous_map.to_ae_eq_fun_add_hom Î¼).comp (to_continuous_map_add_hom Î± E))
    (by { rintros - âŸ¨f, rflâŸ©, exact mem_Lp f } : _ â‰¤ Lp E p Î¼),
end

variables (ğ•œ : Type*) [fact (1 â‰¤ p)]

/-- The bounded linear map of considering a bounded continuous function on a finite-measure space
as an element of `Lp`. -/
def to_Lp [normed_field ğ•œ] [normed_space ğ•œ E] :
  (Î± â†’áµ‡ E) â†’L[ğ•œ] (Lp E p Î¼) :=
linear_map.mk_continuous
  (linear_map.cod_restrict
    (Lp.Lp_submodule E p Î¼ ğ•œ)
    ((continuous_map.to_ae_eq_fun_linear_map Î¼).comp (to_continuous_map_linear_map Î± E ğ•œ))
    mem_Lp)
  _
  Lp_norm_le

lemma coe_fn_to_Lp [normed_field ğ•œ] [normed_space ğ•œ E] (f : Î± â†’áµ‡ E) :
  to_Lp p Î¼ ğ•œ f =áµ[Î¼] f := ae_eq_fun.coe_fn_mk f _

variables {ğ•œ}

lemma range_to_Lp [normed_field ğ•œ] [normed_space ğ•œ E] :
  ((linear_map.range (to_Lp p Î¼ ğ•œ : (Î± â†’áµ‡ E) â†’L[ğ•œ] Lp E p Î¼)).to_add_subgroup)
    = measure_theory.Lp.bounded_continuous_function E p Î¼ :=
range_to_Lp_hom p Î¼

variables {p}

lemma to_Lp_norm_le [nontrivially_normed_field ğ•œ] [normed_space ğ•œ E]:
  â€–(to_Lp p Î¼ ğ•œ : (Î± â†’áµ‡ E) â†’L[ğ•œ] (Lp E p Î¼))â€– â‰¤ (measure_univ_nnreal Î¼) ^ (p.to_real)â»Â¹ :=
linear_map.mk_continuous_norm_le _ ((measure_univ_nnreal Î¼) ^ (p.to_real)â»Â¹).coe_nonneg _

lemma to_Lp_inj {f g : Î± â†’áµ‡ E} [Î¼.is_open_pos_measure] [normed_field ğ•œ] [normed_space ğ•œ E] :
  to_Lp p Î¼ ğ•œ f = to_Lp p Î¼ ğ•œ g â†” f = g :=
begin
  refine âŸ¨Î» h, _, by tautoâŸ©,
  rw [â†fun_like.coe_fn_eq, â†(map_continuous f).ae_eq_iff_eq Î¼ (map_continuous g)],
  refine (coe_fn_to_Lp p Î¼ ğ•œ f).symm.trans (eventually_eq.trans _ $ coe_fn_to_Lp p Î¼ ğ•œ g),
  rw h,
end

lemma to_Lp_injective [Î¼.is_open_pos_measure] [normed_field ğ•œ] [normed_space ğ•œ E] :
  function.injective â‡‘(to_Lp p Î¼ ğ•œ : (Î± â†’áµ‡ E) â†’L[ğ•œ] (Lp E p Î¼)) := Î» f g hfg, (to_Lp_inj Î¼).mp hfg

end bounded_continuous_function

namespace continuous_map

variables [compact_space Î±] [is_finite_measure Î¼]
variables (ğ•œ : Type*) (p Î¼) [fact (1 â‰¤ p)]

/-- The bounded linear map of considering a continuous function on a compact finite-measure
space `Î±` as an element of `Lp`.  By definition, the norm on `C(Î±, E)` is the sup-norm, transferred
from the space `Î± â†’áµ‡ E` of bounded continuous functions, so this construction is just a matter of
transferring the structure from `bounded_continuous_function.to_Lp` along the isometry. -/
def to_Lp [normed_field ğ•œ] [normed_space ğ•œ E] :
  C(Î±, E) â†’L[ğ•œ] (Lp E p Î¼) :=
(bounded_continuous_function.to_Lp p Î¼ ğ•œ).comp
  (linear_isometry_bounded_of_compact Î± E ğ•œ).to_linear_isometry.to_continuous_linear_map

variables {ğ•œ}

lemma range_to_Lp [normed_field ğ•œ] [normed_space ğ•œ E] :
  (linear_map.range (to_Lp p Î¼ ğ•œ : C(Î±, E) â†’L[ğ•œ] Lp E p Î¼)).to_add_subgroup
    = measure_theory.Lp.bounded_continuous_function E p Î¼ :=
begin
  refine set_like.ext' _,
  have := (linear_isometry_bounded_of_compact Î± E ğ•œ).surjective,
  convert function.surjective.range_comp this (bounded_continuous_function.to_Lp p Î¼ ğ•œ),
  rw â†bounded_continuous_function.range_to_Lp p Î¼,
  refl,
end

variables {p}

lemma coe_fn_to_Lp [normed_field ğ•œ] [normed_space ğ•œ E] (f : C(Î±,  E)) :
  to_Lp p Î¼ ğ•œ f =áµ[Î¼] f :=
ae_eq_fun.coe_fn_mk f _

lemma to_Lp_def [normed_field ğ•œ] [normed_space ğ•œ E] (f : C(Î±, E)) :
  to_Lp p Î¼ ğ•œ f
  = bounded_continuous_function.to_Lp p Î¼ ğ•œ (linear_isometry_bounded_of_compact Î± E ğ•œ f) :=
rfl

@[simp] lemma to_Lp_comp_to_continuous_map [normed_field ğ•œ] [normed_space ğ•œ E] (f : Î± â†’áµ‡ E) :
  to_Lp p Î¼ ğ•œ f.to_continuous_map
  = bounded_continuous_function.to_Lp p Î¼ ğ•œ f :=
rfl

@[simp] lemma coe_to_Lp [normed_field ğ•œ] [normed_space ğ•œ E] (f : C(Î±, E)) :
  (to_Lp p Î¼ ğ•œ f : Î± â†’â‚˜[Î¼] E) = f.to_ae_eq_fun Î¼ :=
rfl

lemma to_Lp_injective [Î¼.is_open_pos_measure] [normed_field ğ•œ] [normed_space ğ•œ E] :
  function.injective â‡‘(to_Lp p Î¼ ğ•œ : C(Î±, E) â†’L[ğ•œ] (Lp E p Î¼)) :=
(bounded_continuous_function.to_Lp_injective _).comp
  (linear_isometry_bounded_of_compact Î± E ğ•œ).injective

lemma to_Lp_inj {f g : C(Î±, E)} [Î¼.is_open_pos_measure] [normed_field ğ•œ] [normed_space ğ•œ E] :
  to_Lp p Î¼ ğ•œ f = to_Lp p Î¼ ğ•œ g â†” f = g :=
(to_Lp_injective Î¼).eq_iff

variables {Î¼}

/-- If a sum of continuous functions `g n` is convergent, and the same sum converges in `Láµ–` to `h`,
then in fact `g n` converges uniformly to `h`.  -/
lemma has_sum_of_has_sum_Lp {Î² : Type*} [Î¼.is_open_pos_measure] [normed_field ğ•œ] [normed_space ğ•œ E]
  {g : Î² â†’ C(Î±, E)} {f : C(Î±, E)} (hg : summable g)
  (hg2 : has_sum (to_Lp p Î¼ ğ•œ âˆ˜ g) (to_Lp p Î¼ ğ•œ f)) : has_sum g f :=
begin
  convert summable.has_sum hg,
  exact to_Lp_injective Î¼ (hg2.unique ((to_Lp p Î¼ ğ•œ).has_sum $ summable.has_sum hg)),
end

variables (Î¼) [nontrivially_normed_field ğ•œ] [normed_space ğ•œ E]

lemma to_Lp_norm_eq_to_Lp_norm_coe :
  â€–(to_Lp p Î¼ ğ•œ : C(Î±, E) â†’L[ğ•œ] (Lp E p Î¼))â€–
  = â€–(bounded_continuous_function.to_Lp p Î¼ ğ•œ : (Î± â†’áµ‡ E) â†’L[ğ•œ] (Lp E p Î¼))â€– :=
continuous_linear_map.op_norm_comp_linear_isometry_equiv _ _

/-- Bound for the operator norm of `continuous_map.to_Lp`. -/
lemma to_Lp_norm_le :
  â€–(to_Lp p Î¼ ğ•œ : C(Î±, E) â†’L[ğ•œ] (Lp E p Î¼))â€– â‰¤ (measure_univ_nnreal Î¼) ^ (p.to_real)â»Â¹ :=
by { rw to_Lp_norm_eq_to_Lp_norm_coe, exact bounded_continuous_function.to_Lp_norm_le Î¼ }

end continuous_map

end
